namespace ca.mcgill.ecse439.pizzadeliverysystem.model;

external interface Serializable {}

class Pizza
{

  depend java.io.Serializable;
  isA Serializable;

  name;
  Integer calorieCount;
  Integer price;

  1 -> * Ingredient;
  
  before constructor {
  	if (aName == null || aName.trim().equals("")) {
  		throw new RuntimeException("Please provide a valid name.");
  	}
  	if (aCalorieCount <= 0) {
  		throw new RuntimeException("Please provide a valid calorie count (non-zero positive integer).");
  	}
  	if (aPrice <= 0) {
  		throw new RuntimeException("Please provide a valid price (non-zero positive integer).");
  	}
  }
  
  before setName {
  	if (aName == null || aName.trim().equals("")) {
  		return false;
  	}
  }
  before setCalorieCount {
  	if (aCalorieCount <= 0) {
  		return false;
  	}
  }
  before setPrice {
  	if (aPrice <= 0) {
  		return false;
  	}
  }
}

class MenuPizza
{
  isA Pizza;
}

class CustomPizza
{
  isA Pizza;
  Integer basePrice;
}

class Ingredient
{

  depend java.io.Serializable;
  isA Serializable;  

  name;
  Integer price;
}

class Customer
{

  depend java.io.Serializable;
  isA Serializable;

  name;
  delAddress;
  contact;
}

class DeliveryManager
{

  depend java.io.Serializable;
  isA Serializable;

  singleton;
  
  1 -> * Order orders;
  1 -> * Ingredient ingredients; // all the possible ingredients that can go on a pizza, allows for customer selection
  1 -> 1 Menu menu;
  
  after constructor {
  	menu = new Menu();
  }
  // without this after construct, the constructor does not define a Menu, it only declares it
}

class Order
{

  depend java.io.Serializable;
  isA Serializable;

  autounique id;
  Integer totalAmount;
  boolean delivered;
  
  1 -> * Pizza orderedPizzas;
  1 -> 1 Customer customer;
}

class Menu
{

  depend java.io.Serializable;
  isA Serializable;

  1 -> * Pizza pizzas;
}
